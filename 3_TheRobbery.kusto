//Building arrays
//Getting all the VINs that were moving at the time of the robbery
let DuringRobbery = 
Traffic
| where Timestamp between (datetime(2022-10-16 08:17:00) .. datetime(2022-10-16 08:30:59));
//Starting main processing
//Getting all the VINs that were moving around the bank after the robbery
let AfterRobbery =
Traffic
| where Ave == "157" and Street == "148"
| where Timestamp between (datetime(2022-10-16 08:31:00) .. datetime(2022-10-16 08:39:59));
//Getting all the VINs that were in the area of the robbery after the robbery, but not during the robbery
let PossibleCars = 
AfterRobbery
| join kind=leftanti DuringRobbery on $left.VIN == $right.VIN
| summarize by VIN;
//Getting all the movement hits from the cameras for the cars that were identified in the previous step
Traffic
| where Timestamp > (datetime(2022-10-16 08:40:00))
| where VIN in (PossibleCars)
//Getting the LAST movement hit for each car
| summarize arg_max(Timestamp, *) by VIN
//Using count to identify the location that has seen the most hits by different cars
| summarize Hitcount = count() by Ave, Street
//Filtering by the location that has 3 hits exactly
| where Hitcount == 3
//Joining the main table to the identified value to find out the cars that were found in that location
| join kind=rightsemi Traffic on $left.Ave == $right.Ave and $left.Street == $right.Street
| where VIN in (PossibleCars)